version 1.0

# OptitypeDNA
# The input files are either a pair of fastq files or a bam file 
#

workflow Optimised_OptitypeDna {
  input {
    String sample_basename
    File? input_fq1
    File? input_fq2
    File? input_bam
  }
  
  if ( defined(input_bam) ) {
     call BamToFastq {
       input:
          sample_basename = sample_basename,
          input_bam = input_bam
     }
  }
  
  call OptitypeDnaPrepareHLA as PrepareHLA_fwd {
  input:
       optitype_name = sample_basename,
       input_fq = select_first([input_fq1, BamToFastq.fwd_fastq])
  }
  
  call OptitypeDnaPrepareHLA as PrepareHLA_rev {
  input:
       optitype_name = sample_basename,
       input_fq = select_first([input_fq2, BamToFastq.rev_fastq])
  }
  
  call OptitypeDnaPipeline {
  input:
       optitype_name = sample_basename,
       hla_fwd_fastq = PrepareHLA_fwd.hla_fastq,
       hla_rev_fastq = PrepareHLA_rev.hla_fastq
  }
  
  output {
       File? optitype_tsv = OptitypeDnaPipeline.optitype_tsv
       File? optitype_plot = OptitypeDnaPipeline.optitype_plot
  }
}


task BamToFastq {
  input {
    String sample_basename
    File? input_bam
  }
  
  runtime {
    docker: "alesmaver/bwa_samtools_picard"
    requested_memory_mb_per_core: 8000
    cpu: 2
    runtime_minutes: 700
  }
  
  command <<<
    set -e -o pipefail
    name=~{sample_basename}
    echo $name
    echo Converting BAM to Fastqs
    java  -Xmx14g -jar /usr/picard/picard.jar  SamToFastq INPUT=~{input_bam} FASTQ=$name.fwd.fq.gz SECOND_END_FASTQ=$name.rev.fq.gz
  >>>

output {
    File fwd_fastq = sample_basename + ".fwd.fq.gz"
    File rev_fastq = sample_basename + ".rev.fq.gz"
  }
}


task OptitypeDnaPrepareHLA {
  input {
    String optitype_name
    File input_fq

  }

  #Int space_needed_gb = 10 + round(5*size([input_fq1, input_fq2], "GB"))
  runtime {
    memory: "64GB"
    docker: "mgibio/immuno_tools-cwl:1.0.1"
    #disks: "local-disk ~{space_needed_gb} SSD"
    #bootDiskSizeGb: 3*space_needed_gb
  }

  command <<<
    set -e -o pipefail
    dnaref="/ref_data/optitype_ref/hla_reference_dna.fasta"    # Optitype DNA reference file
    name=~{optitype_name}
    echo $name

    echo Aligning forward reads to reference HLA locus sequence
    /usr/local/bin/bwa mem -t 4 $dnaref ~{input_fq} > $name.aln.sam      # use bwa mem, store output IN TEMP, and skip samse step
    
    echo Select only the mapped reads from the sam files:
    /opt/samtools/bin/samtools view -S -F 4 $name.aln.sam > $name.aln.map.sam
    rm -f  $name.aln.sam

    echo Convert sam files to fastq files, also stored in temp dir
    cat $name.aln.map.sam | grep -v ^@ | /usr/bin/awk '{print "@"$1"\n"$10"\n+\n"$11}' > $name.hla.fastq
    rm -f $name.aln.map.sam

  >>>

  output {
    File hla_fastq = optitype_name + ".hla.fastq"
  }
}

task OptitypeDnaPipeline {
  input {
    String optitype_name
    File hla_fwd_fastq
    File hla_rev_fastq
  }

  #Int space_needed_gb = 10 + round(5*size([hla_fwd_fastq, hla_rev_fastq], "GB"))
  runtime {
    memory: "64GB"
    docker: "mgibio/immuno_tools-cwl:1.0.1"
    #disks: "local-disk ~{space_needed_gb} SSD"
    #bootDiskSizeGb: 3*space_needed_gb
  }

  command <<<
    set -e -o pipefail
    dnaref="/ref_data/optitype_ref/hla_reference_dna.fasta"    # Optitype DNA reference file
    name=~{optitype_name}
    echo $name
    
    echo run Optitype
    # run optitype 
    /usr/bin/python /usr/local/bin/OptiType/OptiTypePipeline.py -i ~{hla_fwd_fastq} ~{hla_rev_fastq} --dna -v -p $name -o .

    mv ./${name}_result.tsv ./${name}.optitype_result.tsv
    mv ./${name}_coverage_plot.pdf ./${name}.optitype_coverage_plot.pdf
  >>>

  output {
    File optitype_tsv = optitype_name + ".optitype_result.tsv"
    File optitype_plot = optitype_name + ".optitype_coverage_plot.pdf"
  }
}
